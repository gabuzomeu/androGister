buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

apply plugin: 'java'
apply plugin: 'maven'  

sourceCompatibility = 1.6 

def generatedModelResources = "gen-src-models"


repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile('de.greenrobot:greendao-generator:1.3.1-ttbox')
    compile('de.greenrobot:greendao:1.3.3')

    compile('com.google.android:android:4.1.1.4')

    compile project(':domain-generator:model-builder')

}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java',  'gen-src-models']
            println "### java.srcDirs = ${java.srcDirs}"
        }
    }
}


task( generateModels, type: JavaExec ) {
    // Config exec
    main = "eu.ttbox.androgister.domain.generator.RegisterDaoGenerator"
    classpath = sourceSets.main.compileClasspath
    // Config Generator
    def schemaVersion = 1
    def schemaPackage = "eu.ttbox.androgister.domain" 
    // Generate
    def modelDest = file("$generatedModelResources")
    def copyDest = file("$generatedModelResources/eu/ttbox/androgister/domain")
    outputs.dir modelDest
    doFirst {
        modelDest.exists() || modelDest.mkdirs()
        copyDest.exists() || copyDest.mkdirs()
        println "--- --------------------------------------------------------- ---"
        println "--- Copy  Template Model in : " +  copyDest
        copy {
            from 'src/main/template'
            into  copyDest
        }
        println "--- --------------------------------------------------------- ---"
        println "--- Generate Product Model in : " +  modelDest
        args 1, "$schemaPackage", "$generatedModelResources" 
        println "--- --------------------------------------------------------- ---"
    } 
  
}
 
compileJava{
    dependsOn generateModels
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources' 
    from sourceSets.main.allSource
}


artifacts {
    archives jar
    archives sourcesJar
}
